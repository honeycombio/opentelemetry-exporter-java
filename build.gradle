plugins {
    id 'java-library'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
	mavenCentral()
    jcenter()
}

dependencies {
    api 'io.honeycomb.libhoney:libhoney-java:1.3.1'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'io.opentelemetry:opentelemetry-api:0.8.0'
    implementation 'io.opentelemetry:opentelemetry-sdk:0.8.0'
    compile 'org.apache.httpcomponents:httpclient:4.5.3'
    compile 'org.apache.httpcomponents:httpasyncclient:4.1.4'

    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.mockito:mockito-core:3.5.13'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.5.13'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			groupId = 'io.honeycomb.opentelemetry'
            artifactId = 'opentelemetry-exporter-java'
            version = '0.1.0'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Honeycomb OpenTelemetry Exporter for Java'
                description = 'Exports OpenTelemetry data to Honeycomb.'
                url = 'https://docs.honeycomb.io/getting-data-in/open-standards/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Honeycomb'
                        name = 'Honeycomb'
                        email = 'support@honeycomb.io'
                        organization = 'Honeycomb'
                        organizationUrl = 'https://honeycomb.io'
                    }
                }
                scm {
                    url = 'https://github.com/honeycombio/opentelemetry-exporter-java'
                    connection = 'scm:git:git@github.com:honeycombio/opentelemetry-exporter-java.git'
                    developerConnection = 'scm:git:git@github.com:honeycombio/opentelemetry-exporter-java.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			if (hasProperty ('sonatype.username'))
				credentials {
					username sonatype.username
					password sonatype.password
				}
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

task install(dependsOn: publishToMavenLocal) {
    group = 'Publishing'
    description = 'Installs artifacts to local Maven repository'
}
